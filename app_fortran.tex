
%--------------------------------------------------
\subsection{Full matrix multiplications in fortran}

In fortran there is the intrinsic function {\sl matmul}. However, it turns out that 
it is not always the fastest option to carry out (full) matrix multiplications.

This code is designed to test this:

\begin{lstlisting}[language=Fortran]
program test
implicit none
! The order of the square matrices is 2000.
integer(kind=4)::n=1000
! Calculate the matrix multiplications:
! i) c:=a*b in a triple do-loop.
! ii) d:=a*b by matmul(a,b).
! iii) e:=a*b by dgemm in INTEL MKL.
real(kind=8),allocatable::a(:,:),b(:,:),c(:,:),d(:,:),e(:,:)
real(kind=8)::alpha,beta
integer(kind=4)::i,j,k,lda,ldb,lde
real(kind=8)::start,finish

allocate(a(n,n),b(n,n),c(n,n),d(n,n),e(n,n))
alpha=1.0;beta=1.0
lda=n;ldb=n;lde=n

! Generate the matrices, a and b, randomly.
call cpu_time(start)
call random_seed()
do j=1, n
do i=1, n
call random_number(a(i,j))
call random_number(b(i,j))
enddo
enddo
call cpu_time(finish)
write(unit=6,fmt=100) "The generation of two matrices takes ",finish-start," seconds."

! i) c:=a*b in a triple do-loop.
call cpu_time(start)
c=0.0D0
do j=1, n
do i=1, n
do k=1, n
c(i,j)=c(i,j)+a(i,k)*b(k,j)
enddo
enddo
enddo
call cpu_time(finish)
write(unit=6,fmt=100) "A triple do-loop takes ",finish-start," seconds."

! ii) d:=a*b by matmul(a,b).
call cpu_time(start)
d=0.0D0
d=matmul(a,b)
call cpu_time(finish)
write(unit=6,fmt=100) "A matmul(a,b) function takes ",finish-start," seconds."

! iii) e:=a*b by dgemm in INTEL MKL.
call cpu_time(start)
e=0.0D0
call dgemm("N","N",n,n,n,alpha,a,lda,b,ldb,beta,e,lde)
call cpu_time(finish)
write(unit=6,fmt=100) "A DGEMM subroutine takes ",finish-start," seconds."

deallocate(a,b,c,d,e)

stop
100 format(A,F8.3,A)
end program test
\end{lstlisting}

It is compiled as follows:
\begin{verbatim}
> gfortran -O3 prog.f90 -lblas
\end{verbatim}

For $100\times100$ matrices:
\begin{verbatim}
The generation of two matrices takes    0.004 seconds.
A triple do-loop takes    0.009 seconds.
A matmul(a,b) function takes    0.001 seconds.
A DGEMM subroutine takes    0.000 seconds.
\end{verbatim}

For $1000\times1000$ matrices:
\begin{verbatim}
The generation of two matrices takes    0.123 seconds.
A triple do-loop takes    1.527 seconds.
A matmul(a,b) function takes    0.080 seconds.
A DGEMM subroutine takes    0.054 seconds.
\end{verbatim}

For $1000\times2000$ matrices:
\begin{verbatim}
The generation of two matrices takes    0.392 seconds.
A triple do-loop takes   33.785 seconds.
A matmul(a,b) function takes    0.725 seconds.
A DGEMM subroutine takes    0.455 seconds.
\end{verbatim}


%--------------------------------------------------
\subsection{A simple example of an Interface}

\begin{lstlisting}[language=Fortran]
program kwadraat

implicit none

integer, parameter :: IntegerRoot = 6
real,    parameter :: RealRoot = 4.5

Interface Square  
    function RealSquare(root)
        real :: root
        real :: RealSquare
    end function

    function IntegerSquare(root)
        integer :: root
        integer :: IntegerSquare
    end function
end interface


write(*,*) "Integer square: ", Square(IntegerRoot)
write(*,*) "Real square:    ", Square(realRoot)

end program

function IntegerSquare(root)
    implicit none
    integer :: root, IntegerSquare
    IntegerSquare = root**2
end function

function RealSquare(root)
    implicit none
    real :: root, RealSquare
    RealSquare = root*root
end function
\end{lstlisting}
