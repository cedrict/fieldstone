#gnuplot script
set terminal pdf enhanced font "12pt,Times"
set pointsize .4
set grid
set xlabel 'T (C)'

############################################################## 
set ylabel '{/Symbol h} (mPa s)'
set output 'viscosity_water.pdf'
A=0.02939
B=507.88
C=149.3 
plot[0:100][]\
'wiki_viscosity_water.ascii' w lp t 'Wikipedia',\
A*exp(B/((x+273)-C)) t 'Vogel-Fulcher-Tammann',\
(1+1.55112e-2*(x-20))**(-1.572) t 'Holzbecher, 1998'

 
############################################################## 
a=  9.998396e2
b=  6.764771e-2
c= -8.993699e-3
d=  9.143518e-5
e= -8.907391e-7
f=  5.291959e-9
g= -1.359813e-11

set ylabel '{/Symbol r} (kg/m^3)'
set output 'density_water.pdf'
plot[:100][]\
'wiki_density_water.ascii' w lp t 'Wikipedia',\
'eng_density_water.ascii'  u 1:3 w lp t 'engineeringtoolbox',\
1000*(1-2e-4*((x+273.15)-(273.15+4))) t 'lin: {/Symbol a}=2e-4',\
a+b*x+c*x**2+d*x**3+e*x**4+f*x**5+g*x**6 t 'App. C, FEFLOW'





############################################################## 
set ylabel '{/Symbol a} (10^{-4} K^{-1})'
set output 'thermal_expansion_water.pdf'
plot[:100][]\
'eng_density_water.ascii'  u 1:9 w lp t 'engineeringtoolbox'


############################################################## 
set ylabel 'k (W/m/K)'
set output 'heat_conductivity_water.pdf'
plot[:100][]\
'eng_hcond_water.ascii' w lp t 'engineeringtoolbox'
