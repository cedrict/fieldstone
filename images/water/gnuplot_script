#gnuplot script
set terminal pdf enhanced font "12pt,Times"
set pointsize .5
set grid
set xlabel 'T (C)'
set key outside
############################################################## 
set ylabel '{/Symbol h} (mPa s)'
set output 'viscosity_water.pdf'
A=0.02939
B=507.88
C=149.3 
plot[0:100][]\
'wiki_viscosity_water.ascii' w lp t 'Wikipedia',\
A*exp(B/((x+273)-C)) t 'Vogel-Fulcher-Tammann',\
(1+1.55112e-2*(x-20))**(-1.572) t 'Pawlowski, 1991' #(holzbecher book)

 
############################################################## 
#wooding 1957
f(x)=996.9*(1-3.17e-4*(x-298.15)-2.56e-6*(x-298.15)**2 )

#Thiesen et al 1900
g(x)=1000*(1-(x-3.98)**2/503570*(x+283)/(x+67.26))

#Tilton et al 1937
h(x)=1000*(1-(x-3.98)**2/508929.2*(x+288.9414)/(x+68.12963))


a=  9.998396e2
b=  6.764771e-2
c= -8.993699e-3
d=  9.143518e-5
e= -8.907391e-7
f=  5.291959e-9
g= -1.359813e-11

set ylabel '{/Symbol r} (kg/m^3)'
set output 'density_water.pdf'
plot[:100][]\
'wiki_density_water.ascii' w lp t 'Wikipedia',\
'eng_density_water.ascii'  u 1:3 w lp t 'engineeringtoolbox',\
1000*(1-2e-4*((x+273.15)-(273.15+4))) t 'lin: {/Symbol a}=2e-4',\
a+b*x+c*x**2+d*x**3+e*x**4+f*x**5+g*x**6 t 'App. C, FEFLOW',\
f(x+273.15) t 'Wooding, 1957',\
g(x) t 'Thiesen et al, 1900',\
h(x) t 'Tilton et al, 1937 '




############################################################## 
set ylabel '{/Symbol a} (10^{-4} K^{-1})'
set output 'thermal_expansion_water.pdf'
plot[:100][]\
'eng_density_water.ascii'  u 1:9 w lp t 'engineeringtoolbox'


############################################################## 
set ylabel 'k (W/m/K)'

T0=273.15
f(x)=(-922.47+2839.5*((x+T0)/T0)-1800.7*((x+T0)/T0)**2+525.77*((x+T0)/T0)**3-73.44*((x+T0)/T0)**4)/1000. # holzbecher book

set output 'heat_conductivity_water.pdf'
plot[0:350][]\
'eng_hcond_water.ascii' w lp t 'engineeringtoolbox',\
f(x) t 'Yusa & Oishi'

############################################################## 
set ylabel 'Cp (J/kg/K)'

#holzbecher book
f(x)=(3.3774-1.12665e-2*(x+273.15)+1.34687e-5*(x+273.15)**2)*4187.6

set output 'heat_capacity_water.pdf'
plot[0:350][0:7000]\
f(x) t 'Yusa & Oishi',\
4200



